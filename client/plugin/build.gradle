import groovy.json.JsonBuilder

// Apply the publishing plugin.
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.7"
    }
}
apply plugin: "com.gradle.plugin-publish"
apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'ivy-publish'
apply plugin: 'checkstyle'

version = "0.1"
group = "com.banderous.upm"

pluginBundle {
    website = 'https://banderous.github.io/upm'
    vcsUrl = 'https://github.com/banderous/upm'
    description = 'A Unity package manager'
    tags = ['Unity', 'Package manager']

    plugins {
        upmPlugin {
            id = 'com.banderous.upm'
            displayName = 'Game Package Manager'
        }
    }
}

sourceSets {
    integrationTest {
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integTest/groovy')
        }
        resources.srcDir file('src/integTest/resources')
    }
}

configurations {
    archives
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

repositories {
    jcenter()
}

checkstyle {
    toolVersion = "7.4"
    ignoreFailures = false
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile "org.codehaus.groovy:groovy-all:2.4.7"
    compile "com.google.code.gson:gson:2.8.0"
    compile 'com.google.guava:guava:20.0'
    compile 'org.yaml:snakeyaml:1.17'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'commons-io:commons-io:2.5'


    testCompile gradleTestKit()
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4-rc-3') {
        exclude module: 'groovy-all'
    }
    testCompile 'org.apache.commons:commons-compress:1.12'

    archives project(path: ':client:unityPuppet', configuration: 'archives')
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

task getPuppet(type: Copy, dependsOn: ':client:unityPuppet:build') {
    from configurations.archives
    into 'src/main/resources'
}

classes.dependsOn getPuppet

publishing {
    repositories {
        ivy {
            // change to point to your repo, e.g. http://my.org/repo
            url "${rootProject.buildDir}/repo"
        }
    }
    publications {
        ivy(IvyPublication) {
            from components.java
            artifact(jar) {
                type "source"
                conf "runtime"
            }
        }
    }
}

// Run only tests annotated with @Trouble.
task testTrouble(type: Test) {
    systemProperty 'test.trouble', true
}

// Run only integration tests annoted with @Trouble.
task integrationTrouble(type: Test) {
    systemProperty 'test.trouble', true
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}
